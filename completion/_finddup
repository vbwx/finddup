#compdef finddup

# Completion function for zsh

_arguments -S -s \
	'-a[compare all files (including hidden files)]' \
	'-e[ignore empty files]' \
	'(-r -R)'{-r,-R}'[look for duplicates in subdirectories as well]' \
	'-I+[only compare files matching the glob pattern]:glob:_files' \
	'-X+[do not compare files matching the glob pattern]:glob:_files' \
	'-h[do not regard multiple hard links to the same file as duplicates]' \
	'-i[ignore case of glob patterns]' \
	'-q[hide progress indicator and number of duplicated/unique files]' \
	'(-p)-x[triple the sample size]' \
	'-0[print paths with NUL characters as separators]' \
	'(- :)--version' \
	'(- :)--help' \
	'*:file or directory:_files' \
	+ '(method)' \
		'(-s -S -2 -4 -8 -x)-p[compare the entire contents of files]' \
		'-t[trim repeating characters from the beginning and end of files]' \
	+ '(accuracy)' \
		'(-p)-2[compare files using twice as many samples]' \
		'(-p)-4[compare files using four times as many samples]' \
		'(-p)-8[compare files using eight times as many samples]' \
	+ '(mode)' \
		'-d[print path of each file with a list of paths of its duplicates]' \
		'-l[print paths of each file and its duplicate on separate lines]' \
		'-o[only print paths of files that are duplicates of other files]' \
		'-O[only print paths of files that have at least one duplicate]' \
		'(-p)-s[only print paths of smaller duplicates]' \
		'(-p)-S[only print paths of larger duplicates]' \
		'-c[only print paths of least recently changed duplicates]' \
		'-C[only print paths of most recently changed duplicates]' \
		'-m[only print paths of least recently modified duplicates]' \
		'-M[only print paths of most recently modified duplicates]' \
		'-v[only print paths of least recently accessed duplicates]' \
		'-V[only print paths of most recently accessed duplicates]' \
		'-n[only print paths of files that have no duplicates]' \
	+ '(symlinks)' \
		'-H[follow symbolic links on the command line]' \
		'-L[follow all symbolic links]' \
		'-P[do not follow symbolic links]' \
	+ '(filetype)' \
		'-B[only compare binary files]' \
		'-T[only compare text files]' \
