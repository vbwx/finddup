#!/usr/bin/env perl

use strict;
use 5.018;
use Getopt::Std;
use File::Find;
use File::Compare;
use File::stat;
use File::Spec::Functions qw(abs2rel canonpath splitpath file_name_is_absolute);
use Cwd qw(getcwd realpath);
use Sort::Key::Natural qw(natsort natkeysort);
use List::Util 'reduce';
use Text::Glob 'match_glob';
use Fcntl ':seek';
use subs qw(die warn);

use constant {
	NO => 0,
	YES => 1,
	PATH => 0,
	SIZE => 1,
	COMPARE => 1,
	TIME => 2,
	EX_USAGE => 2,
	PROGRESS_LEN => 70
};

$Getopt::Std::STANDARD_HELP_VERSION = YES;
$main::VERSION = '1.12.6';
our ($opt_l, $opt_o, $opt_O, $opt_R, $opt_r, $opt_p, $opt_t, $opt_B, $opt_T, $opt_m, $opt_M, $opt_n);
our ($opt_i, $opt_a, $opt_s, $opt_S, $opt_e, $opt_q, $opt_H, $opt_L, $opt_P, $opt_I, $opt_X, $opt_0);
our ($opt_d, $opt_c, $opt_C, $opt_v, $opt_V, $opt_h, $opt_x, $opt_2, $opt_4, $opt_8);

my $CWD = getcwd();
my @dir = ();
my @files = ();
my %files = ();
my %paths = ();
my %inodes = ();
my %dupes = ();
my $samples = 6;
my $sample_size = 5;
my $found = 0;
my $comps = 0;
my $total;
my $all = YES;

getopts('dloOsSmMncCvV0x248RrqptBTaehiHLPI:X:') or pod2usage(EX_USAGE);
if ($opt_l + $opt_o + $opt_O + $opt_s + $opt_S + $opt_c + $opt_C + $opt_m + $opt_M +
	$opt_v + $opt_V + $opt_d + $opt_n > 1) {
	pod2usage(-message => "Mutually exclusive options: ".($opt_c && 'c ').($opt_C && 'C ').
		($opt_d && 'd ').($opt_l && 'l ').($opt_m && 'm ').($opt_M && 'M ').($opt_n && 'n ').
		($opt_o && 'o ').($opt_O && 'O ').($opt_s && 's ').($opt_S && 'S ').
		($opt_v && 'v ').($opt_V && 'V'), -exitval => EX_USAGE);
}
if ($opt_p && ($opt_t || $opt_s || $opt_S)) {
	pod2usage(-message => "Mutually exclusive options: p ".($opt_S && 'S ').($opt_s && 's ').
		($opt_t && 't'), -exitval => EX_USAGE);
}
if ($opt_2 + $opt_4 + $opt_8 > 1) {
	pod2usage(-message => "Mutually exclusive options: ".($opt_2 && '2 ').($opt_4 && '4 ').
		($opt_8 && '8'), -exitval => EX_USAGE);
}
if ($opt_p && $opt_x) {
	warn "Option -x has no effect when -p is specified";
}
if ($opt_p && ($opt_2 || $opt_4 || $opt_8)) {
	warn "Option -".($opt_2 ? 2 : ($opt_4 ? 4 : 8))." has no effect when -p is specified";
}
if ($opt_B && $opt_T) {
	pod2usage(-message => "Mutually exclusive options: B T", -exitval => EX_USAGE);
}

my $interactive = !$opt_q && -t && -t STDERR;
$| = YES;
$opt_T = $opt_B = YES unless $opt_T || $opt_B;
$opt_s ||= $opt_S;
$opt_c ||= $opt_C;
$opt_m ||= $opt_M;
$opt_v ||= $opt_V;
$opt_r ||= $opt_R;
$opt_t ||= $opt_s;
$Text::Glob::strict_leading_dot = NO if $opt_a;

if ($opt_P) {
	$opt_H = $opt_L = NO;
}
else {
	$opt_H ||= $opt_L;
}
if ($opt_i) {
	$opt_I = fc $opt_I;
	$opt_X = fc $opt_X;
}
if ($opt_0) {
	$\ = "\0";
	$opt_o = YES unless $opt_O || $opt_d || $opt_l || $opt_s || $opt_c || $opt_m || $opt_v || $opt_n;
}
else {
	$\ = "\n";
}
if ($opt_2) {
	$samples *= 2;
}
elsif ($opt_4) {
	$samples *= 4;
}
elsif ($opt_8) {
	$samples *= 8;
}
if ($opt_x) {
	$sample_size *= 3;
}
die "Default output mode does not allow redirection" unless -t STDOUT ||
	$opt_o || $opt_O || $opt_d || $opt_l || $opt_s || $opt_c || $opt_m || $opt_v || $opt_n;

for (@ARGV) {
	if (-l) {
		unless ($opt_H) {
			warn "$_: Symbolic link skipped";
			$all = NO;
			next;
		}
		$_ = (file_name_is_absolute($_) ? realpath($_) : abs2rel(realpath($_)));
	}
	else {
		$_ = canonpath($_);
	}
	my $p = realpath($_);
	if (defined $paths{$p}) {
		warn "'$paths{$p}' and '$_' are identical";
		next;
	}
	$paths{$p} = $_;
	if (-f) {
		my ($t, $s, $dup, $stat, $time);
		$t = -T _ unless $opt_T && $opt_B;
		$all = NO;
		unless ((!$t && $opt_B) || ($t && $opt_T)) {
			warn "$_: ".($t ? "Text" : "Binary")." file skipped";
			next;
		}
		$s = -s _;
		if ($opt_e && !$s) {
			warn "$_: Empty file skipped";
			next;
		}
		undef $dup;
		$stat = File::stat::populate(CORE::stat(_));
		if ($stat->nlink > 1 && $inodes{$stat->dev}{$stat->ino}) {
			if ($opt_h) {
				warn "'".$inodes{$stat->dev}{$stat->ino}[PATH]."' and '$_' are identical";
				next;
			}
			$dup = $inodes{$stat->dev}{$stat->ino};
		}
		if ($opt_t) {
			push(@files, [$_, YES]); # [PATH, COMPARE]
			$inodes{$stat->dev}{$stat->ino} = [$_] if !$dup && $stat->nlink > 1; # [PATH]
		}
		else {
			$time = ($opt_m ? $stat->mtime : ($opt_c ? $stat->ctime : ($opt_v ? $stat->atime : '')));
			my $e = [$_, YES, $time]; # [PATH, COMPARE, TIME]
			if ($dup) {
				add_dup($dup, $e);
			}
			else {
				push(@{$files{$s}}, $e);
				$inodes{$stat->dev}{$stat->ino} = $e if $stat->nlink > 1;
			}
		}
	}
	elsif (-d _) {
		push(@dir, $_);
	}
	else {
		die "$_: No such file or directory";
	}
}
@dir = ('.') if !@dir && $all;
print_traversal() if @dir;

find({
	follow => $opt_L,
	follow_skip => 2,
	preprocess => sub {natsort(@_)},
	wanted => sub {
		my ($f, $p, $s, $t, $name, $dup, $stat, $time);
		$f = ($opt_i ? fc : $_);
		if (!-f || (!$opt_a && /^[.~]|\r$|^Thumbs\.db$/) ||
			($opt_X && match_glob($opt_X, $f)) || ($opt_I && !match_glob($opt_I, $f))) {
			$File::Find::prune = YES unless $opt_r || $_ eq '.';
			return;
		}
		$s = -s _;
		return if $opt_e && !$s;
		$t = -T _ unless $opt_T && $opt_B;
		return unless (!$t && $opt_B) || ($t && $opt_T);
		if (-l) {
			return unless $opt_L && $File::Find::fullname;
			(undef, undef, $f) = splitpath($File::Find::fullname);
			return if !$opt_a && $f =~ /^[.~]|\r$|^Thumbs\.db$/;
			$f = fc $f if $opt_i;
			return if ($opt_X && match_glob($opt_X, $f)) || ($opt_I && !match_glob($opt_I, $f));
			$name = (file_name_is_absolute($File::Find::name) ?
				$File::Find::fullname : abs2rel($File::Find::fullname, $CWD));
		}
		else {
			$name = $File::Find::name =~ s/^\.\///r;
		}
		$p = realpath($_);
		return if $paths{$p};
		$paths{$p} = YES;
		if ($opt_t) {
			$found++;
			push(@files, [$name, $all]); # [PATH, COMPARE]
		}
		else {
			undef $dup;
			$stat = stat $_;
			if ($stat->nlink > 1 && $inodes{$stat->dev}{$stat->ino}) {
				return if $opt_h;
				$dup = $inodes{$stat->dev}{$stat->ino};
			}
			$time = ($opt_m ? $stat->mtime : ($opt_c ? $stat->ctime : ($opt_v ? $stat->atime : '')));
			my $e = [$name, $all, $time]; # [PATH, COMPARE, TIME]
			if ($dup) {
				$found++;
				add_dup($dup, $e);
			}
			elsif ($all || $files{$s}) {
				$found++ if $files{$s};
				push(@{$files{$s}}, $e);
				$inodes{$stat->dev}{$stat->ino} = $e if $stat->nlink > 1;
			}
		}
		print_traversal();
	}
}, @dir);

%inodes = %paths = ();
$found = 0;
clear_progress() if @dir;
print_progress();

if ($opt_p) {
	$total = reduce {my $n = @$b - 1; $a + ($n > 0 ? $n*($n + 1)/2 : 0)} (0, values %files)
		if $interactive;
	while (my ($s, $f) = each %files) {
		if (@$f > 1) {
			for my $i (0 .. $#$f-1) {
				if ($$f[$i][COMPARE]) {
					my $del = NO;
					for my $j ($i+1 .. $#$f) {
						if (defined $$f[$j][PATH] && compare($$f[$i][PATH], $$f[$j][PATH]) == 0) {
							if ($opt_n) {
								$del = YES;
							}
							else {
								add_dup($$f[$i], $$f[$j]);
							}
							$found++;
							$total -= $#$f - $j;
							delete $$f[$j];
						}
						$comps++;
						print_progress();
					}
					delete $$f[$i] if $del;
				}
			}
		}
	}
}
elsif ($opt_t) {
	%inodes = ();
	$total = @files;
	for (@files) {
		my $dup;
		my $s = -s $$_[PATH];
		my $stat = File::stat::populate(CORE::stat(_));
		$comps++;
		if ($stat->nlink > 1 && $inodes{$stat->dev}{$stat->ino}) {
			next if $opt_h;
			$dup = $inodes{$stat->dev}{$stat->ino};
		}
		my $time = ($opt_m ? $stat->mtime : ($opt_c ? $stat->ctime : ($opt_v ? $stat->atime : '')));
		my $digest = ($dup || trim_digest($$_[PATH], $s)) or next;
		if ($$_[COMPARE] || $dupes{$digest}) {
			$found++ if $dupes{$digest};
			push(@{$dupes{$digest}}, [$$_[PATH], $s, $time]); # [PATH, SIZE, TIME]
		}
		$inodes{$stat->dev}{$stat->ino} = $digest if !$dup && $stat->nlink > 1;
		undef $dup;
		print_progress();
	}
}
else {
	$total = reduce {$a + (@$b > 1 ? @$b : 0)} (0, values %files) if $interactive;
	while (my ($s, $f) = each %files) {
		if (@$f > 1) {
			my %m = ();
			while (each @$f) {
				$comps++;
				my $d = digest($$f[$_][PATH], $s) or next;
				if (defined $m{$d}) {
					if ($opt_n) {
						delete @$f[$m{$d}, $_];
					}
					else {
						add_dup($$f[$m{$d}], $$f[$_]);
					}
					$found++;
				}
				elsif ($$f[$_][COMPARE]) {
					$m{$d} = $_;
				}
				print_progress();
			}
		}
	}
}
clear_progress(YES);

if ($opt_n) {
	my @f;
	if ($opt_t) {
		while (each %dupes) {
			push(@f, $dupes{$_}[0][PATH]) if @{$dupes{$_}} == 1;
		}
	}
	else {
		push(@f, map {$$_[PATH]} grep {$_ && @$_ && !$dupes{$$_[PATH]}} @{$files{$_}})
			while each %files;
	}
	print for natsort(@f);
	$found = scalar @f;
}
else {
	$found = 0;
	for (natkeysort {$dupes{$_}[0][PATH]} keys %dupes) {
		my $d = $dupes{$_};
		next unless @$d > 1;
		if ($opt_C || $opt_M || $opt_V) {
			@$d = sort {$$a[TIME] <=> $$b[TIME]} @$d;
		}
		elsif ($opt_c || $opt_m || $opt_v) {
			@$d = sort {$$b[TIME] <=> $$a[TIME]} @$d;
		}
		elsif ($opt_S) {
			@$d = sort {$$a[SIZE] <=> $$b[SIZE]} @$d;
		}
		elsif ($opt_s) {
			@$d = sort {$$b[SIZE] <=> $$a[SIZE]} @$d;
		}
		if ($opt_o || $opt_s || $opt_c || $opt_m || $opt_v) {
			print $$d[$_][PATH] for 1 .. $#$d;
		}
		elsif ($opt_O) {
			print $$d[0][PATH];
		}
		elsif ($opt_l) {
			for (1 .. $#$d) {
				print $$d[$_][PATH];
				print $$d[0][PATH];
			}
		}
		elsif ($opt_d) {
			print $$d[0][PATH];
			print(($opt_0 ? '' : "\t").$$d[$_][PATH]) for 1 .. $#$d;
			print '';
		}
		else {
			my $stat0 = stat $$d[0][PATH];
			for (1 .. $#$d) {
				my $stat = stat $$d[$_][PATH];
				print $$d[$_][PATH].($stat->dev == $stat0->dev && $stat->ino == $stat0->ino ?
					' === ' : ($opt_p ? ' == ' : ' ~~ ')).$$d[0][PATH];
			}
		}
		$found += @$d - 1;
	}
}

print_info(($found ? (-t STDOUT ? "\n" : '').$found : 'No'),
	($found && !$opt_n ? ($opt_p ? ' exact' : ' possible') : ''),
	($opt_n ? ' unique file' : ' duplicate'), ($found == 1 ? '' : 's'));
exit !$found;


sub add_dup {
	my ($orig, $dup) = @_;
	$dupes{$$orig[PATH]} = [$orig] unless $dupes{$$orig[PATH]};
	if ($dupes{$$dup[PATH]}) {
		push(@{$dupes{$$orig[PATH]}}, @{$dupes{$$dup[PATH]}});
		delete $dupes{$$dup[PATH]};
	}
	else {
		push(@{$dupes{$$orig[PATH]}}, $dup);
	}
}

sub digest {
	my ($path, $size) = @_;
	my ($file, $d, $data, $s);
	return 'x' unless $size;
	if (open($file, '<', $path)) {
		$s = int(($size - $samples*$sample_size)/($samples - 1));
		if ($s > 0) {
			for (1 .. $samples) {
				seek($file, $s, SEEK_CUR) if $_ > 1;
				read($file, $d, $sample_size) or last;
				$data .= $d;
			}
		}
		elsif (read($file, $d, $size)) {
			$data = $d;
		}
		close $file;
		return $data;
	}
	else {
		warn "$path: Permission denied";
		return '';
	}
}

sub trim_digest {
	my ($path, $size) = @_;
	my ($file, $data, $d, $s, $c);
	return 'x' unless $size;
	if (open($file, '<', $path)) {
		$s = 0;
		$data = $c if read($file, $c, 1);
		$s++ while read($file, $d, 1) && $c eq $d;
		$size -= $s;
		if ($size > 1) {
			seek($file, -1, SEEK_END);
			$data .= $c if read($file, $c, 1);
			$size-- while seek($file, -2, SEEK_CUR) && read($file, $d, 1) && $c eq $d;
			if ($size > 2) {
				seek($file, $s + 1, SEEK_SET);
				$s = int(($size - 2 - $samples*$sample_size)/($samples - 1));
				if ($s > 0) {
					for (1 .. $samples) {
						seek($file, $s, SEEK_CUR) if $_ > 1;
						read($file, $d, $sample_size) or last;
						$data .= $d;
					}
				}
				elsif (read($file, $d, $size - 2)) {
					$data .= $d;
				}
			}
		}
		close $file;
		return pack('J', $size).$data;
	}
	else {
		warn "$path: Permission denied";
		return '';
	}
}

sub print_info {
	say STDERR @_ unless $opt_q;
	return;
}

sub print_traversal {
	return unless $interactive;
	state $last_found = -1;
	state $directories = (@dir > 1 || $opt_r ? 'directories' : 'directory');
	state $file = ($opt_t ? 'file' : 'potential duplicate');
	if ($found > $last_found) {
		$last_found = $found;
		printf STDERR ("\rTraversing $directories (%d $file%s found) ", $found,
			($found == 1 ? '' : 's'));
	}
}

sub print_progress {
	return unless $interactive;
	state $last_prog = -1;
	state $last_found = 0;
	my $prog = ($total ? int(100*$comps/$total) : 0);
	if ($prog > $last_prog || $found > $last_found) {
		$last_prog = $prog;
		$last_found = $found;
		printf STDERR ("\r%3d%% of %d comparison%s completed (%d duplicate%s found)", $prog,
			$total, ($total == 1 ? '' : 's'), $found, ($found == 1 ? '' : 's'));
	}
}

sub clear_progress {
	my ($newline) = @_;
	printf STDERR ("\r%s%s", ' ' x PROGRESS_LEN, ($newline ? "\n" : '')) if $interactive;
}

sub pod2usage {
	eval 'use open qw(:locale :std)';
	require Pod::Usage;
	goto &Pod::Usage::pod2usage;
}

sub HELP_MESSAGE {
	pod2usage(-verbose => 1);
	return;
}

sub die {
	my ($msg) = @_;
	say STDERR "finddup: $msg";
	exit 1;
}

sub warn {
	my ($msg) = @_;
	printf STDERR ("%s%-${\PROGRESS_LEN}s\n", ($interactive ? "\r" : ''), "finddup: $msg");
	return;
}

__END__

=encoding UTF-8

=head1 NAME

B<finddup> E<ndash> Finds duplicated files fast and efficiently.

=head1 SYNOPSIS

=for markdown <!--

=over 8

=item B<finddup>

S<[B<-aehiqrx0>] [B<-p> | B<-t>] [B<-2> | B<-4> | B<-8>] [B<-B> | B<-T>] [B<-H> | B<-L> | B<-P>]>
S<[B<-d> | B<-l> | B<-o> | B<-O> | B<-s> | B<-S> | B<-c> | B<-C> | B<-m> | B<-M> | B<-v> | B<-V> | B<-n>]>
S<[B<-I> I<glob>] [B<-X> I<glob>] [I<file> ...]>

=back

=for markdown -->
    finddup [-aehiqrx0] [-p | -t] [-2 | -4 | -8] [-B | -T] [-H | -L | -P]
            [-d | -l | -o | -O | -s | -S | -c | -C | -m | -M | -v | -V | -n]
            [-I glob] [-X glob] [file ...]

=head1 DESCRIPTION

This utility compares the contents of files to check if any of them match.
What is considered a match depends on the chosen method.

=over 2

=item *

By default, files are compared B<heuristically>, which means that files are
considered duplicates if they are the same size, and if a few bytes of
different parts of the file contents (samples) are identical to their
counterparts.

This method is very fast and accurate enough for most use cases, but it can
produce false positives (or false negatives when invoked with B<-n>). The
number of samples that are compared can be increased with the B<-2>, B<-4>,
and B<-8> options, which can reduce the number of false matches but also
increase the run time, especially for the trim method. The sample size can be
increased with the B<-x> option.

=item *

The B<trim> method (B<-t>) also employs heuristic comparison as
described above, but it ignores repeating characters at the start and
end of file contents. This is especially useful for text files, which often
end with blank lines, and video files, which might have a varying number of
NUL characters at the end of their contents.

However, this method is slower because it needs to open every file to compare
their contents to each other, whereas the default method only has to compare
files of the same size.

=item *

With B<precise comparison> (B<-p>), file contents are compared
byte for byte, so it can be guaranteed that only perfect duplicates are found.

This method is the slowest one unless all files are different sizes, in which
case it is actually faster than the trim method.

=back

Note that multiple hard links to the same file are considered duplicates
unless the B<-h> option is specified.

There are various output modes that are mostly useful for subsequent
processing of the results.

=over 2

=item *

By default, duplicates and their originals are shown in pairs, separated by
one of the following equality signs: C<~~> means that the files are probably
duplicates; C<==> indicates that the file contents are identical; C<===>
means that their inode numbers are identical.

The format of this output mode might change in the future and is therefore not
suited for automatic processing or piping. B<finddup> prevents output
redirection in this mode.

=item *

The B<-d> option prints the path of each file along with a tab-indented list
of the paths of its duplicates.

=item *

The B<-l> option prints the paths of each file and its duplicate on separate
lines.

=item *

The B<-o> option prints all duplicates of other files, whereas B<-O> prints
the I<original> files, i.e., the files that were encountered first and found
to have duplicates.

=item *

The B<-s> and B<-S> options print the smallest and largest duplicates,
respectively. Since this only makes sense when used with the B<trim> method,
these options automatically activate it.

=item *

The B<-m> and B<-M> options print the least and most recently modified
duplicates, respectively. B<-c> and B<-C> do the same but they look at inode
change time, whereas B<-v> and B<-V> look at access time.

=item *

The B<-n> option negates the results, meaning that only the paths of files
that do not have duplicates are printed.

=back

As for non-option arguments, B<finddup> differentiates between files and
directories; files passed as arguments are checked and compared first, and
directories are traversed after. Hence, while it does not matter whether
files or directories appear first on the command line, the order of multiple
files and the order of multiple directories might affect the results,
depending on the output mode.

When invoked without non-option arguments, B<finddup> looks for duplicates
in the working directory. When files are passed as arguments, B<finddup>
only looks for duplicates of these files.

This manual contains a L<tutorial|/TUTORIAL>.

=head1 OPTIONS

=head2 Comparison Methods

=over

=item B<-p>

Compare the entire contents of files.
This is slower but only considers files to be duplicates if they are perfect
matches.

=item B<-t>

Trim repeating characters from the beginning and end of file contents
before comparing them.

=back

=head2 Comparison Accuracy

=over

=item B<-2>

Use twice as many samples for heuristic comparison.

=item B<-4>

Use four times as many samples for heuristic comparison.

=item B<-8>

Use eight times as many samples for heuristic comparison.

=item B<-x>

Use three times as many bytes per sample for heuristic comparison.

=back

=head2 Output Modes

=over

=item B<-d>

Print path of each file with a list of paths of its duplicates.

If combined with the B<-0> (zero) option, each path is terminated with a NUL
character, while the last path in the list of duplicates is terminated with
two NUL characters.

=item B<-l>

Print paths of each file and its duplicate on separate lines.

=item B<-o>

Only print paths of files that are duplicates of other files.
This corresponds to the path on the left in the default output mode.

=item B<-O>

Only print paths of files that have at least one duplicate.
This corresponds to the path on the right in the default output mode.

=item B<-s>

Only print paths of files whose size is smaller than or equal to
the size of their respective duplicates.

Implies B<-t>.

=item B<-S>

Only print paths of files whose size is larger than or equal to
the size of their respective duplicates.

Implies B<-t>.

=item B<-c>

Only print paths of files whose inode change time is older than
or equal to the time of their respective duplicates.

=item B<-C>

Only print paths of files whose inode change time is newer than
or equal to the time of their respective duplicates.

=item B<-m>

Only print paths of files whose modification time is older than
or equal to the time of their respective duplicates.

=item B<-M>

Only print paths of files whose modification time is newer than
or equal to the time of their respective duplicates.

=item B<-v>

Only print paths of files whose access time is older than
or equal to the time of their respective duplicates.

=item B<-V>

Only print paths of files whose access time is newer than
or equal to the time of their respective duplicates.

=item B<-n>

Only print paths of files that have no duplicates.

=back

=head2 Directory Traversal Options

=over

=item B<-a>

Compare all files, including hidden files, such as F<Thumbs.db>
and F<Icon?>. Also look for files in hidden directories.

=item B<-e>

Ignore empty files.

=item B<-r>, B<-R>

Look for duplicates in subdirectories as well.

=item B<-B>

Only compare binary files.

=item B<-T>

Only compare text files.

=item B<-h>

Do not compare files whose inode numbers are identical.

=item B<-H>

Follow symbolic links on the command line.

This option has no effect on Microsoft Windows.

=item B<-L>

Follow all symbolic links.

This option has no effect on Microsoft Windows.

=item B<-P>

Do not follow symbolic links. This is the default.

=item B<-I> I<glob>

Only compare files matching the pattern I<glob>.

=item B<-X> I<glob>

Do not compare files matching the pattern I<glob>.

=item B<-i>

Ignore the case of glob patterns.

=back

=head2 Miscellaneous Options

=over

=item B<-q>

Do not print the number of duplicated or unique files.
Hide the progress indicator.

=item B<-0>

Print paths separated by NUL characters; useful for S<C<xargs -0>>.

Implies B<-o> unless an L<output mode|/"Output Modes"> is specified.

=item B<--help>

Print a synopsis of the command and its options.

=item B<--version>

Print version information.

=back

=head1 NOTES

The B<finddup> command accepts the B<--> option, which will cause it to stop
processing flag options. This allows you to pass file or directory names that
begin with a dash (C<->).

=head1 EXIT STATUS

The B<finddup> utility exits 0 on success, 1 if no duplicates were found,
and greater than 0 if an error occurs.

=head1 TUTORIAL

For all of these examples you should bear in mind that, unless B<-p> is
specified, this utility might identify duplicates that are not, in fact,
identical but you have to trade off precision against speed of operation.

In this tutorial, the words I<duplicates> and I<copies> are used
interchangeably.

=head2 Finding Duplicates

Let's start by looking for duplicates in the working directory.

    finddup

You can also check whether a directory contains duplicates of files in
another directory (or vice versa). Note that this command will also find
copies of files that are both located in the same directory.

    finddup dir1 dir2

To simply get a list of duplicates (without the corresponding original file),
call C<finddup -o dir1 dir2> instead. Provided that F<dir2> contains copies
of files from F<dir1>, this command will print the paths of the duplicated
files in F<dir2>.

=head2 Comparing Files

You might want to find out which files are copies of other files.

    finddup file1.xyz file2.xyz file3.xyz

The next example shows how to determine which of two files is the original,
i.e., the older one of the duplicates, provided that they are perfectly
identical.

    finddup -pm file1.xyz file2.xyz

=head2 Removing Duplicates

It's easy to pipe the results to another utility, e.g., to delete
duplicated files. (The B<-0> (zero) option implies B<-o> unless another
L<output mode|/"Output Modes"> is specified, which comes in handy for
a simple operation like this.)

    finddup -0 | xargs -0 rm

However, maybe you only want to delete specific files that already exist
somewhere else and leave all other duplicates untouched, if there are any.
This command searches F<dir> recursively, and either does nothing or
removes F<file.xyz> if a duplicate of it exists anywhere in F<dir>. (It
will also try to delete the file more than once if F<dir> contains multiple
copies of it.)

    finddup -rO0 file.xyz dir | xargs -0 rm

You could also delete text files that are almost identical but end (or
begin) with unnecessary blank lines.

    finddup -TS0 | xargs -0 rm

B<Caution:> In the examples above, I<heuristic> comparison was used, which
could lead to the removal of files that were not exact copies of any other
file but that the utility still regarded as duplicates. Only the I<precise
comparison method> can rule out false positives.

=head2 Finding Unique Files

You might find yourself in a situation where two or more directories contain
the same files except for a few that have been changed (or corrupted). To get
a list of these unique files, you can negate the results.

    finddup -n dir1 dir2

Similarly, to make sure that the working directory does not contain a copy of
a specific file, you can use a command like this.

    finddup -n . file.xyz

=head2 Including and Excluding Files

You can specify which files should be compared or skipped during directory
traversal. Let's say you don't want backup files to be compared.

    finddup -X "*.bak"

You could also, e.g., look for duplicated video and audio files in the working
directory and all its subdirectories recursively. (The pattern in the command
below matches filenames with the extensions C<mp3>, C<mp4>, C<m4a>, C<m4v>,
C<mkv>, etc. The B<-i> option makes patterns case-insensitive.)

    finddup -ri -I "*.{mp[34],m?[av]}"

You can even combine inclusion and exclusion patterns. This command compares
all JPEG files except the ones whose filenames contain C<_thumb>.

    finddup -ri -I "*.{jpg,jpeg}" -X "*_thumb.*"

Consult the documentation of L<Text::Glob> for a detailed explanation of
pattern syntax.

=head1 CAVEATS

Although B<finddup> should work on any platform, it has so far only been
tested on macOS.

=head1 SEE ALSO

L<diff(1)>, L<xargs(1)>, L<File::Compare>, L<Text::Glob>

=head1 AUTHORS

=for markdown <!--

Bernhard Waldbrunner L<https://github.com/vbwx>

=for markdown -->
Bernhard Waldbrunner ([github.com/vbwx](https://github.com/vbwx))

=cut
